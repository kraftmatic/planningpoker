buildscript {
    ext {
        springBootVersion = '1.3.3.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'net.researchgate.release' version '2.3.4'
    id 'com.craigburke.bower-installer' version '2.5.1'
}

apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: 'idea'
apply plugin: 'eclipse'

jar {
    baseName = 'planningpoker'
}

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    mavenCentral()
}

ext['tomcat.version'] = '7.0.59' // needed for Java 6 compatibility

dependencies {
    compile('com.google.guava:guava:19.0')
    compile('org.apache.commons:commons-lang3:3.4')
    compile('org.projectlombok:lombok:1.16.6')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile("org.springframework.boot:spring-boot-starter-websocket")
    compile("org.springframework:spring-messaging")
    compile('org.springframework.boot:spring-boot-actuator')
    compile('org.springframework.boot:spring-boot-devtools')
    testCompile('org.springframework.boot:spring-boot-starter-test')
}

bower {
    installBase = 'src/main/resources/static/bower'
    'angular'('1.4.x')
    'Chart.js'('1.1.1')
    'angular-chart'('https://github.com/jtblin/angular-chart.js.git')
    'angular-poller'('0.4.2')
}


eclipse {
    classpath {
        containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
        containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.6'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}

task stage(dependsOn: ["bowerInstall", "build"]) {}

