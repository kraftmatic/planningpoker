buildscript {
    ext {
        springBootVersion = '1.3.3.RELEASE'
    }
    repositories {
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'net.researchgate.release' version '2.3.4'
    id 'com.craigburke.client-dependencies' version '1.1.4'
}

apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'jacoco'

jar {
    baseName = 'planningpoker'
}

springBoot {
    executable = true
}

repositories {
    jcenter()
}

sourceCompatibility = 1.6

ext['tomcat.version'] = '7.0.59' // needed for Java 6 compatibility

dependencies {
    compile('com.google.guava:guava:19.0')
    compile('org.apache.commons:commons-lang3:3.4')
    compile('org.projectlombok:lombok:1.16.6')
    compile('org.springframework:spring-messaging')
    compile('org.springframework.boot:spring-boot-starter-websocket')
    compile('org.springframework.boot:spring-boot-devtools')
    compile group: 'net.bull.javamelody', name: 'javamelody-core', version: '1.32.1'

    testCompile('org.springframework.boot:spring-boot-starter-test')
}

clientDependencies {
    npm {
        installDir = 'src/main/resources/static/clientlibs'
        'stomp-websocket'('https://github.com/jmesnil/stomp-websocket.git')
        'sockjs'('https://github.com/sockjs/sockjs-client.git')
        'angular'('1.5.x')
        'angular-chart'('https://github.com/jtblin/angular-chart.js.git')
        'Chart.js'('https://github.com/chartjs/Chart.js.git')
    }
}

tasks.processResources.dependsOn(["clientInstall"])

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

eclipse {
    classpath {
        containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
        containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.6'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}

task stage(dependsOn: ["build"]) {}
